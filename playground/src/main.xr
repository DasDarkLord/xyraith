event startup {
    asyncCall :setupWorld 0
    asyncCall :makeItems 0
}

;; Set up world
function :setupWorld {
    foreach :x (range -20 20) {
        foreach :z (range -20 20) {
            world.setBlock (loc (load :x) 64 (load :z)) minecraft:grass_block
        }
    }
    foreach :x (range -10 10) {
        foreach :z (range -10 10) {
            foreach :y (range 32 64) {
                world.setBlock (loc (load :x) (load :y) (load :z)) minecraft:bedrock
            }
        }
    }
    foreach :x (range -9 9) {
        foreach :z (range -9 9) {
            foreach :y (range 33 64) {
                world.setBlock (loc (load :x) (load :y) (load :z)) minecraft:stone
                store :randomOre (random 1 1000)
                if (ge (load :randomOre) 950) {
                    world.setBlock (loc (load :x) (load :y) (load :z)) minecraft:coal_ore
                }
                if (ge (load :randomOre) 980) {
                    world.setBlock (loc (load :x) (load :y) (load :z)) minecraft:iron_ore
                }
                if (ge (load :randomOre) 995) {
                    world.setBlock (loc (load :x) (load :y) (load :z)) minecraft:diamond_ore
                }
            }
        }
    }
}

;; Create items
function :makeItems {
    global.store :pickaxe (item minecraft:wooden_pickaxe)
    global.store :pickaxe (item.setName (global.load :pickaxe) "<white><bold>PICKAXE")

    global.store :cobble (item minecraft:cobblestone)
    global.store :cobble (item.setName (global.load :cobble) "<gray><bold>COBBLESTONE")

    global.store :coal (item minecraft:coal)
    global.store :coal (item.setName (global.load :coal) "<dark_gray><bold>COAL")

    global.store :iron (item minecraft:iron_ingot)
    global.store :iron (item.setName (global.load :iron) "<#cccccc><bold>IRON")

    global.store :diamond (item minecraft:diamond)
    global.store :diamond (item.setName (global.load :diamond) "<aqua><bold>DIAMOND")
}

event join {
    player.giveItems (global.load :pickaxe)
}

event breakBlock {
    store :material (event.block.getBlock)
    if (eq (load :material) minecraft:stone) {
        player.giveItems (global.load :cobble)
    }
    if (eq (load :material) minecraft:coal_ore) {
        player.giveItems (global.load :coal)
    }
    if (eq (load :material) minecraft:iron_ore) {
        player.giveItems (global.load :iron)
    }
    if (eq (load :material) minecraft:diamond_ore) {
        player.giveItems (global.load :diamond)
    }
}

event placeBlock {
    event.setCancelled (true)
}